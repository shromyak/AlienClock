apply plugin: 'com.android.application'

def gcmKey() {
    def apid
    new File("ignore_me.str").withReader { apid = it.readLine() }
    return apid
}

android {
    compileSdkVersion 23
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId "com.svyat.sample.alienclock"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"
        resValue "string", "google_app_id", gcmKey()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def retrofitVersion = '2.1.0'
def gmslibVersion = '8.4.0'
def supVersion = '23.4.0'
def junitVersion = '4.12'
def powermockVersion = '1.6.2'
def runnerVersion = '0.5'
def multidexVersion = '1.0.1'
def daggerVersion = '1.2.5'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    testCompile "junit:junit:$junitVersion"
    testCompile "org.powermock:powermock-module-junit4:$powermockVersion"
    testCompile "org.powermock:powermock-module-junit4-rule:$powermockVersion"
    testCompile "org.powermock:powermock-api-mockito:$powermockVersion"
    testCompile "org.powermock:powermock-classloading-xstream:$powermockVersion"

    compile "com.squareup.dagger:dagger:$daggerVersion"
    compile "com.squareup.dagger:dagger-compiler:$daggerVersion"
    compile "com.android.support:appcompat-v7:$supVersion"
    compile "com.android.support:design:$supVersion"
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile ("com.squareup.retrofit2:converter-simplexml:$retrofitVersion") {
        exclude module: 'stax-api'
        exclude module: 'stax'
        exclude module: 'xpp3'
    }
    compile "com.google.android.gms:play-services:$gmslibVersion"
    compile "com.android.support:multidex:$multidexVersion"

    androidTestCompile "junit:junit:$junitVersion"
    androidTestCompile "com.android.support.test:runner:$runnerVersion"
    androidTestCompile "com.android.support:support-annotations:$supVersion"
}
